module hvsync_generator
(
    input wire clk, reset,
    output wire hsync, vsync, video_on, p_tick,
    output wire [9:0] pixel_x, pixel_y
);

   localparam H_Disp = 640; // Actual horizontal display in pixels
   localparam V_Disp = 480; // Actual vertical display in pixels

   localparam H_Front = 48; // The bumper in the front (horizontal/left)
   localparam V_Front = 10; // The bumper in the front (vertical/up)

   localparam H_Back = 16;  // The bumper in the back (horizontal/right)
   localparam V_Back = 33;  // The bumber in the back (vertical/down)

   localparam V_Retrace = 2; // default for CRT monitor
   localparam H_Retrace = 96; // default for CRT monitor

   reg mod2_reg;
   wire mod2_next;
   reg [9:0] h_count_reg, h_count_next;
   reg [9:0] v_count_reg, v_count_next;
   reg v_sync_reg, h_sync_reg;
   wire v_sync_next, h_sync_next;
   wire h_end, v_end, pixel_tick;

   always @(posedge clk, posedge reset)
      if (reset)
         begin
            mod2_reg <= 1'b0;
            v_count_reg <= 0;
            h_count_reg <= 0;
            v_sync_reg <= 1'b0;
            h_sync_reg <= 1'b0;
         end
      else
         begin
            mod2_reg <= mod2_next;
            v_count_reg <= v_count_next;
            h_count_reg <= h_count_next;
            v_sync_reg <= v_sync_next;
            h_sync_reg <= h_sync_next;
         end

   assign mod2_next = ~mod2_reg;
   assign pixel_tick = mod2_reg;

   assign h_end = (h_count_reg==(H_Disp+H_Front+H_Back+H_Retrace-1));
   assign v_end = (v_count_reg==(V_Disp+V_Front+V_Back+V_Retrace-1));

   always @*
      if (pixel_tick)  // 25 MHz pulse
         if (h_end)
            h_count_next = 0;
         else
            h_count_next = h_count_reg + 1;
      else
         h_count_next = h_count_reg;


   always @*
      if (pixel_tick & h_end)
         if (v_end)
            v_count_next = 0;
         else
            v_count_next = v_count_reg + 1;
      else
         v_count_next = v_count_reg;

	// Make h_sync_next a bool based on if the current h_count is less than the horizontal size of the entire display
   assign h_sync_next = (h_count_reg>=(H_Disp+H_Back) &&
                         h_count_reg<=(H_Disp+H_Back+H_Retrace-1));

 	// Make v_sync_next a bool based on if the current v_count is less than the vertical size of the entire display
   assign v_sync_next = (v_count_reg>=(V_Disp+V_Back) &&
                         v_count_reg<=(V_Disp+V_Back+V_Retrace-1));

   assign video_on = (h_count_reg<H_Disp) && (v_count_reg<V_Disp);

   assign hsync = h_sync_reg;
   assign vsync = v_sync_reg;
   assign pixel_x = h_count_reg;
   assign pixel_y = v_count_reg;
   assign p_tick = pixel_tick;

endmodule


   